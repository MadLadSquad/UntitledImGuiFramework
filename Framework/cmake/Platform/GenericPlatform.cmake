function(multicast fun)
    set(optionals ${ARGN})
    if (NOT EMSCRIPTEN)
        if (NOT UIMGUI_SKIP_FRAMEWORK)
            cmake_language(CALL ${fun} UntitledImGuiFramework ${optionals})
        endif()
        if (NOT WIN32)
            cmake_language(CALL ${fun} ${APP_LIB_TARGET} ${optionals})
        endif()
    endif()
    cmake_language(CALL ${fun} ${APP_TARGET} ${optionals})
endfunction()

macro(enable_objc)
    if (APPLE)
        enable_language(OBJC)
        enable_language(OBJCXX)
    endif()
endmacro()

macro(setup_glfw_vendored_generic flag)
    if (${flag})
        add_subdirectory("${UIMGUI_SRC_PREFIX}/Framework/ThirdParty/glfw")
    endif()
    set(GLFW_INCLUDE_DIRS "${UIMGUI_SRC_PREFIX}/Framework/ThirdParty/glfw/include/")
    set(GLFW_LIBRARIES_T "glfw")
endmacro()

macro(setup_glfw_system)
    pkg_check_modules(GLFW REQUIRED glfw3)
    if (BUILD_VARIANT_STATIC)
        set(GLFW_LIBRARIES_T ${GLFW_STATIC_LIBRARIES})
    else()
        set(GLFW_LIBRARIES_T ${GLFW_LIBRARIES})
    endif()
endmacro()

macro(setup_yaml_cpp_vendored_generic force_shared)
    if (${force_shared})
        set(BUILD_SHARED_LIBS ON)
    endif ()
    add_subdirectory("${UIMGUI_SRC_PREFIX}/Framework/ThirdParty/yaml-cpp")
    set(YAML_CPP_LIBRARIES_T "yaml-cpp")
    set(YAML_CPP_INCLUDE_DIRS_T "${UIMGUI_SRC_PREFIX}/Framework/ThirdParty/yaml-cpp/include/yaml-cpp"
            "${UIMGUI_SRC_PREFIX}/Framework/ThirdParty/yaml-cpp/include")
endmacro()

macro(setup_opengl_system_generic)
    find_package(OpenGL REQUIRED)
    set(OPENGL_LIBRARIES_T "${OPENGL_gl_LIBRARY}" "${OPENGL_LIBRARIES}")
endmacro()

macro(setup_vulkan_system)
    pkg_check_modules(Vulkan REQUIRED vulkan)
    set(VULKAN_LIBRARIES_T ${Vulkan_LINK_LIBRARIES})
endmacro()

macro(setup_platform_compilation_plugin_generic)
    target_link_libraries(${UIMGUI_PLUGIN_NAME} PUBLIC "${UIMGUI_PLUGIN_APP_NAME}Lib")
endmacro()

macro(setup_opengl_loader_sources_generic disable_setup)
    if (NOT ${disable_setup})
        list(APPEND FRAMEWORK_INCLUDE_DIRS "${UIMGUI_SRC_PREFIX}/Framework/ThirdParty/glad/include/")
        file(GLOB_RECURSE GLAD_SRC "${UIMGUI_SRC_PREFIX}/Framework/ThirdParty/glad/src/gl.c")
        file(GLOB_RECURSE GLAD_HEAD "${UIMGUI_SRC_PREFIX}/Framework/ThirdParty/glad/include/*.h")
    endif ()
endmacro()

macro(setup_platform_target_settings_generic)
    if (NOT UIMGUI_SKIP_FRAMEWORK)
        target_compile_definitions(UntitledImGuiFramework PRIVATE "UIMGUI_COMPILE_LIB" "YAML_CPP_DLL" "URLL_USE_FUNCTIONAL"
                "MLS_EXPORT_LIBRARY" "MLS_LIB_COMPILE" "VULKAN_HPP_NO_STRUCT_CONSTRUCTORS")
    endif ()
    target_compile_definitions(${APP_TARGET} PRIVATE "UIMGUI_EXECUTABLE")
endmacro()